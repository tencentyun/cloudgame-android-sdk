plugins {
    id 'com.android.application'
}

android {
    namespace 'com.tencent.tcrdemo'
    compileSdk 35
    signingConfigs {
        release {
            storeFile file('../../../../../keystore/local.keystore')
            keyAlias 'tcgsdk'
            keyPassword 'tcgsdk'
            storePassword 'tcgsdk'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    defaultConfig {
        multiDexEnabled true
        applicationId "com.tencent.tcrdemo"
        minSdk 21
        targetSdk 35
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters "arm64-v8a"
        }
    }

    flavorDimensions += "demoType"
    productFlavors {
        full {
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
            buildConfigField 'boolean', 'useLightSdk', "false"
            resValue "string", "app_name", "TcrDemo-Full"
        }
        lite {
            applicationIdSuffix ".lite"
            versionNameSuffix "-lite"
            buildConfigField 'boolean', 'useLightSdk', "true"
            resValue "string", "app_name", "TcrDemo-lite"
        }
        scences {
            applicationIdSuffix ".advance"
            resValue "string", "app_name", "场景demo"
            buildConfigField 'boolean', 'useLightSdk', "false"
        }
        simple {
            applicationIdSuffix ".simple"
            resValue "string", "app_name", "simpledemo"
            buildConfigField 'boolean', 'useLightSdk', "false"
        }
    }

    sourceSets {
        full {
            java.srcDirs = ['src/tcrdemo/java']
            res.srcDirs = [
                    'src/tcrdemo/res'
            ]
            manifest.srcFile 'src/tcrdemo/AndroidManifest.xml'
        }
        lite {
            java.srcDirs = ['src/tcrdemo/java']
            res.srcDirs = [
                    'src/tcrdemo/res'
            ]
            manifest.srcFile 'src/tcrdemo/AndroidManifest.xml'
        }
        scences {
            java.srcDirs = ['src/scenes/java']
            res.srcDirs = [
                    'src/scenes/res'
            ]
            manifest.srcFile 'src/scenes/AndroidManifest.xml'
        }
        simple {
            java.srcDirs = ['src/simple/java']
            res.srcDirs = [
                    'src/simple/res'
            ]
            manifest.srcFile 'src/simple/AndroidManifest.xml'
        }
    }

    buildFeatures {
        buildConfig true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'com.tencent.bugly:crashreport:latest.release'

    if (gradle.ext.use_local_module_TcrSdkFull) {
        fullImplementation project(':TcrSdkFull')
        scencesImplementation project(':TcrSdkFull')
        simpleImplementation project(':TcrSdkFull')
    } else {
        fullImplementation "com.tencent.tcr:tcrsdk-full:$rootProject.tcrsdkVersion"
        scencesImplementation "com.tencent.tcr:tcrsdk-full:$rootProject.tcrsdkVersion"
        simpleImplementation "com.tencent.tcr:tcrsdk-full:$rootProject.tcrsdkVersion"
    }
    if (gradle.ext.use_local_module_TcrSdkLight) {
        liteImplementation project(':TcrSdk')
    } else {
        liteImplementation "com.tencent.tcr:tcrsdk-lite:$rootProject.tcrsdkVersion"
    }
    implementation "com.tencent.tcr:tcr-gamepad:$rootProject.TcrGuiVersion"
    implementation "com.tencent.tcr:proxy:$rootProject.TcrProxyVersion"
}